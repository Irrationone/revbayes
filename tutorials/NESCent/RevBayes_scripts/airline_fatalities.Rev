################################################################################
#
# RevBayes Exercise: Compute the posterior probabilities of a Poisson regression model 
#                               for airline fatalities
# 
# This file: Specifies the full model parameters and moves for the model and runs the analysis
#
# authors: Sebastian Hoehna
#
################################################################################



observed_fatalities <- v(24,25,31,31,22,21,26,20,16,22)
x <- 1976:1985 - mean(1976:1985)

alpha ~ dnUnif(-2,2) 
beta ~ dnUnif(-2,2)

mi <- 0
moves[mi++] <- mvSlide(alpha)
moves[mi++] <- mvSlide(beta)

for (i in 1:x.size() ) {
    lambda[i] := exp( alpha + beta * x[i] )
    y[i] ~ dnPoisson(lambda[i])
    y[i].clamp(observed_fatalities[i])
}

mymodel <- model( alpha )

monitors[1] <- modelmonitor(filename="output/airline_fatalities.log",printgen=10, separator = "	")
monitors[2] <- screenmonitor(printgen=10, alpha, beta)

mymcmc <- mcmc(mymodel, monitors, moves)

mymcmc.burnin(generations=1000,tuningInterval=100)
mymcmc.run(generations=3000)



