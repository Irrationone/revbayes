#
# Tutorial -- the phylogenetic comparative method
# Correlation analysis: rates, GC content, dates and life-history traits in placental mammals
# Nicolas Lartillot -- July 2014
#

######### 
# data
#########

# read the quantitative trait data 
contData <- readCharacterData("data/archaeaTemp.nex")

# get some useful variables from the data
contTaxaCount <- contData.ntaxa()
nTraits <- contData.nchar()[1]

"number of traits to analyze"
nTraits
contData

# read the sequence data
# the readCharacter function returns a vector of matrices. We just take the first one
seqData <- readCharacterData("data/archaeaRNA.nex")

# get some useful variables from the data
taxaCount <- seqData.ntaxa()
names <- seqData.names()
nSites <- seqData.nchar()[1]
"number of sites of the alignment"
nSites

######### 
# tree  
#########

# create a tree whose tips match the taxon names given by the dataset
tau ~ uniformTimeTree( originTime = 1.0, taxonNames = names)

# work under fixed topology
# set tau equal to externally given tree
treeArray <- readTrees("data/chronoarchaea.tree")
fixedTree <- treeArray[1]
tau.setValue(fixedTree)

index <- 1

# moves on the tree: only relative dates
moves[index] <- mSubtreeScale(tau, weight=5.0)
index <- index + 1
moves[index] <- mNodeTimeSlideUniform(tau, weight=10.0)
index <- index + 1

# if we wanted to, we could also move the tree topology
# and integrate over the posterior induced by the sequence data
# but mixing is currently a bit challenging 
# moves[index] <- mNNI(tau, weight=5.0)
# index <- index + 1
# moves[index] <- mFNPR(tau, weight=5.0)
# index <- index + 1

######### 
# traits
#########

# covariance matrix of dimension nTraits + 2
# first entry will be log of substitution rate
# then all other entries will stand for the life-history traits

# covariance matrix is inverse Wishart of parameters kappa * Identity and df = nTraits+3 degrees of freedom
# where kappa is itself a scaling factor

kappa ~ exponential(1.0)
df <- nTraits+4
covdim <- nTraits+2

Sigma ~ invWishart(dim=covdim, kappa=kappa, df=df)

# moves on kappa
moves[index] <- mScale(kappa, lambda=2.0, tune=true, weight=3.0)
index <- index + 1

# multivariate Brownian process along the tree
X ~ mvtBrownian(tau,sigma=Sigma)

# conjugate Gibbs move on covariance matrix
moves[index] <- mvConjugateInverseWishartBrownian(sigma=Sigma, process=X, kappa=kappa, df=df, weight=1) 
index <- index + 1

# moves on the Brownian process
moves[index] <- mvMultivariatePhyloProcessSliding(process=X,lambda=10,tune=true,weight=100) 
index <- index + 1
moves[index] <- mvMultivariatePhyloProcessSliding(process=X,lambda=1,tune=true,weight=100) 
index <- index + 1
moves[index] <- mvMultivariatePhyloProcessTranslation(process=X,lambda=1,tune=true,weight=1) 
index <- index + 1
moves[index] <- mvMultivariatePhyloProcessTranslation(process=X,lambda=0.1,tune=true,weight=1) 
index <- index + 1

# first and second components of the Brownian model map to substitution rate and gc content (see below)
# other copmonents of the Brownian process correspond to life-history traits
# so we clamp the process at the tips of the tree, at the values observed in extant taxa
# you need to do this trait by trait
for (i in 1:nTraits)	{

	# here we say that the (i+2)th entry of the Brownian process should map to the ith. quantitative trait
	X.clampAt(contData,i+2,i)
}

# get useful functions of the covariance matrix
for (i in 1:covdim)	{
	var[i] := Sigma.covariance(i,i)
}

k <- 1;
for (i in 1:(covdim-1))	{
	for (j in (i+1):covdim)	{
		correl[k] := Sigma.correlation(i,j)
		pCorrel[k] := Sigma.partialCorrelation(i,j)
		k <- k+1
	}
}

# get useful outputs from the brownian process

for (i in 1:covdim)	{
	# mean and stdev of the log rate (i=1), logit gc (i=2) and then each trait
	meanX[i] := X.mean(i)
	stdevX[i] := X.stdev(i)
	rootX[i] := X.rootVal(i)
}

######### 
# substitution process
#########

# equilibrium frequencies 
bf <- v(1,1,1,1)
pi ~ dirichlet(bf)

# moves on pi
moves[index] <- mSimplexElementScale(pi, alpha=10.0, tune=true, weight=1.0)
index <- index + 1

# first component of the Brownian model gives the *log* of the substitution rate
branchrates := expbranchtree(tree=tau,process=X,traitindex=1)

# second component of the Brownian model gives the *logit* of the equilibrium GC content
# thus GC = tanh(X_2(t)), where X is the Brownian motion
# tanh is the hyperbolic tangent: tanh(x) = exp(x) / (1 + exp(x))
branchGC := tanhBranchTree(tree=tau,process=X,traitindex=2)

# the transition / transversion ratio
tstv ~ exponential(0.1)

# each branch has its own substitution matrix, which is a T92 matrix
# defined by a transition/transversion ratio and an equilibrium GC
# so, we use branchGC, which gives a GC for each branch
# we combine it with the global ts/tv ratio
branchMatrices := t92GCBranchTree(tree=tau,branchGC=branchGC,tstv=tstv)


# create the sequence evolution model
seq ~ substModel(tree=tau, Q=branchMatrices, rootFrequencies=pi, branchRates=branchrates, nSites=nSites, type="DNA")

# condition on sequence alignment
seq.clamp(seqData)

# create model
mymodel <- model(kappa)

# screen monitor
monitors[1] <- screenmonitor(printgen=10, correl, meanX, rootX)

# similar monitor, but directly into file
monitors[2] <- filemonitor(filename="output/archaeagctemp.trace", printgen=10, separator = "	", correl, var, meanX, rootX)

# file monitors for the correlation and partial correlation coefficients
monitors[3] <- filemonitor(filename="output/archaeagctemp.correl",printgen=10, separator = "	", correl, pCorrel)

# a file monitor just for the covariance matrix
monitors[4] <- filemonitor(filename="output/archaeagctemp.cov",printgen=10, separator = "	", Sigma)

# a file monitor for the evolution of traits along the tree (in newick format)
monitors[5] <- filemonitor(filename="output/archaeagctemp.traits",printgen=10, separator = "	", X)

# a model monitor
monitors[6] <- modelmonitor(filename="output/archaeagctemp.log",printgen=10, separator = "	")
 
mymcmc <- mcmc(mymodel, monitors, moves)

mymcmc.burnin(generations=100,tuningInterval=100)

mymcmc.run(100000)


