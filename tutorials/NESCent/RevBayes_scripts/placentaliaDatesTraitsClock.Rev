#
# Tutorial -- the phylogenetic comparative method
# Correlation analysis: rates, dates and life-history traits in placental mammals
# Nicolas Lartillot -- July 2014
#

######### 
# data
#########

# read the quantitative trait data 
contData <- readCharacterData("data/plac40lhtlog.nex")

# get some useful variables from the data
nTraits <- contData.nchar()[1]

# read the sequence data
# the readCharacter function returns a vector of matrices. We just take the first one
seqData <- readCharacterData("data/plac40_4fold.nex")

# get some useful variables from the data
nTaxa <- seqData.ntaxa()
names <- seqData.names()
nSites <- seqData.nchar()[1]


######### 
# tree  
#########

# create a birth death process, leading to a tree whose tips match the taxon names given by the dataset

speciation ~ exponential(0.1)
extinction ~ exponential(0.1)

# rough estimate of sampling fraction: 40 placental mammals out of the ~4000 currently existing out there
sampling_fraction := 0.01

psi ~ cBDP(lambda=speciation, mu=extinction, rho=sampling_fraction, rootAge=1.0, nTaxa=nTaxa, names=names)

# work under fixed, externally provided, tree topology
treeArray <- readTrees("data/chronoplac40.tree")
fixedTree <- treeArray[1]
psi.setValue(fixedTree)


######### 
# rate and evolution
#########

# covariance matrix of dimension nTraits 
# first entry will be log of substitution rate
# then all other entries will stand for the life-history traits

# covariance matrix is inverse Wishart of parameters kappa * Identity and df = nTraits+2 degrees of freedom
# where kappa is itself a scaling factor

kappa ~ dnLogUniform(min=0.001,max=1000)
kappa.setValue(0.1)
df <- nTraits+2
covdim <- nTraits

Sigma ~ invWishart(dim=covdim, kappa=kappa, df=df)

# multivariate Brownian process along the tree
X ~ mvtBrownian(psi,sigma=Sigma)

# copmonents of the Brownian process correspond to life-history traits
# so we clamp the process at the tips of the tree, at the values observed in extant taxa
# you need to do this trait by trait
for (i in 1:nTraits)	{

	# here we say that the ith entry of the Brownian process should map to the ith. quantitative trait
	X.clampAt(contData,i,i)
}

# get useful functions of the covariance matrix, to be monitored
for (i in 1:covdim)	{
	var[i] := Sigma.covariance(i,i)
}

k <- 1;
for (i in 1:(covdim-1))	{
	for (j in (i+1):covdim)	{
		correl[k] := Sigma.correlation(i,j)
		pCorrel[k] := Sigma.partialCorrelation(i,j)
		k <- k+1
	}
}

# get useful outputs from the brownian process
# the mean across the tree, for each trait
for (i in 1:covdim)	{
	meanX[i] := X.mean(i)
}

######### 
# substitution process
#########

# clock rate
clockrate ~ exponential(0.01)

# equilibrium frequencies 
bf <- v(1,1,1,1)
pi ~ dirichlet(bf)

# exchangeabilities
e <- v(1,1,1,1,1,1)
er ~ dirichlet(e)

# rate matrix
Q := gtr(er,pi)

# create the sequence evolution model
seq ~ substModel(tree=psi, Q=Q, branchRates=clockrate, nSites=nSites, type="DNA")

# condition on sequence alignment
seq.clamp(seqData)

# create model
mymodel <- model(kappa)

######### 
# moves
#########

# initializing index
index <- 1

# rescaling moves on speciation and extinction rates
moves[index] <- mScale(speciation, lambda=1, tune=true, weight=3.0)
index <- index + 1
moves[index] <- mScale(extinction, lambda=1, tune=true, weight=3.0)
index <- index + 1

# moves on the tree: only relative dates
moves[index] <- mSubtreeScale(psi, weight=5.0)
index <- index + 1
moves[index] <- mNodeTimeSlideUniform(psi, weight=10.0)
index <- index + 1

# if we wanted to, we could also move the tree topology
# and integrate over the posterior induced by the sequence data
# but mixing is currently a bit challenging 
# moves[index] <- mNNI(psi, weight=5.0)
# index <- index + 1
# moves[index] <- mFNPR(psi, weight=5.0)
# index <- index + 1

# moves on kappa
moves[index] <- mScale(kappa, lambda=2.0, tune=true, weight=3.0)
index <- index + 1

# conjugate Gibbs move on covariance matrix
moves[index] <- mvConjugateInverseWishartBrownian(sigma=Sigma, process=X, kappa=kappa, df=df, weight=1) 
index <- index + 1

# moves on the Brownian process
moves[index] <- mvMultivariatePhyloProcessSliding(process=X,lambda=1,tune=true,weight=100) 
index <- index + 1
moves[index] <- mvMultivariatePhyloProcessTranslation(process=X,lambda=0.1,tune=true,weight=1) 
index <- index + 1

# moves on clockrate
moves[index] <- mScale(clockrate, lambda=1.0, tune=true, weight=3.0)
index <- index + 1
moves[index] <- mScale(clockrate, lambda=0.1, tune=true, weight=3.0)
index <- index + 1

# moves on the substitution process parameters
moves[index] <- mSimplexElementScale(pi, alpha=10.0, tune=true, weight=1.0)
index <- index + 1
moves[index] <- mSimplexElementScale(er, alpha=10.0, tune=true, weight=1.0)
index <- index + 1

######### 
# monitors
#########

# screen monitor
monitors[1] <- screenmonitor(printgen=10, speciation, extinction, kappa, correl, clockrate, meanX)

# similar monitor, but directly into file
monitors[2] <- filemonitor(filename="output/placdatestraits.trace", printgen=10, separator = "	", speciation, extinction, kappa, correl, var, clockrate, meanX)

# file monitors for the correlation and partial correlation coefficients
monitors[3] <- filemonitor(filename="output/placdatestraits.correl",printgen=10, separator = "	", correl, pCorrel)

# a file monitor just for the covariance matrix
monitors[4] <- filemonitor(filename="output/placdatestraits.cov",printgen=10, separator = "	", Sigma)

# a file monitor for the evolution of traits along the tree (in newick format)
monitors[5] <- filemonitor(filename="output/placdatestraits.traits",printgen=10, separator = "	", X)

# monitoring the trees and dates
monitors[6] <- filemonitor(filename="output/placdatestraits.trees",printgen=10, separator = "	", psi)

# a model monitor
monitors[7] <- modelmonitor(filename="output/placdatestraits.log",printgen=10, separator = "	")
 
mymcmc <- mcmc(mymodel, monitors, moves)

mymcmc.burnin(generations=100,tuningInterval=100)

mymcmc.run(100000)


