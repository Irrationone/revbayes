################################################################################
#
# RevBayes Example: Phylogeny inference using the Jukes-Cantor substitution model.
#
# 
# This file: Specifies the a JC subtitution model with uncorrelated, exponentially
#            distributed branch lengths. The tree is unrooted.
#            Here, we run an MCMC on this model.
#
# authors: Sebastian Hoehna
#
################################################################################



#######################
# Reading in the Data #
#######################

data_atpB <- readCharacterData("data/conifer_atpB.nex")
data_rbcL <- readCharacterData("data/conifer_rbcL.nex")

### Create concatenated data matrix
data <- data_atpB + data_rbcL

# You may want to check some stuff from the data matrix.
# You can always find the available member methods of an object by xxx.methods() where xxx is your variable name.
data


# Get some useful variables from the data. We need these later on.
taxaCount <- data.ntaxa()
nSites <- data.nchar()[1]
names <- data.names()



##############
# Tree model #
##############

# Specify a uniform topology prior
tau ~ dnUniformTopology(taxaCount, names)

# create an index variable for our moves vector
mi <- 0

# create a random variable for each branch length
numNodes <- tau.nnodes()
for (i in 1:numNodes) {
  # We use here the exponential distribution with rate 1.0 as the branch length prior
  brlens[i] ~ dnExponential(10.0)
  
  # Add a move for the branch length. We just take a simple scaling move since the value is a positive real number.
  moves[mi++] <- mvScale(brlens[i]) 
}
# create a deterministic variable for monitoring purposes
tree_length := sum(brlens)


# Build the tree by combining the topology with the branch length.
psi := treeAssembly(tau, brlens)

# moves on the tree
moves[mi++] <- mvNNI(tau)



#######################
# Substitution Models #
#######################


# the rate matrix, here we use the Jukes-Cantor model with 4 states
Q <- JC(4)




###############
# Clock Model #
###############

# we could specify here a rate scalar or something similar, for examble different rate per branch
# in this case we keep the rate multiplier by 1.0
clockRate <- 1.0




###################
# PhyloCTMC Model #
###################


# the sequence evolution model
seq ~ dnPhyloCTMC(tree=psi, Q=Q, branchRates=clockRate, type="DNA")

# attach the data
seq.clamp(data)




#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel <- model(Q)







