#
# Tutorial -- the phylogenetic comparative method
# log-Brownian evolution of body mass in placental mammals
# Nicolas Lartillot -- July 2014
#

# read the data 
contData <- readCharacterData("data/archaeaTemp.nex")

# output a brief summary
contData

# get some useful variables from the data
taxaCount <- contData.ntaxa()
names <- contData.names()

#create a tree whose tips match the taxon names given by the dataset
tau ~ uniformTimeTree( originTime = 1.0, taxonNames = names)

# work under fixed topology
# set tau equal to externally given tree
treeArray <- readTrees("data/chronoarchaea.tree")
fixedTree <- treeArray[1]
tau
fixedTree
tau.setValue(fixedTree)
tau

# sigma: variance per unit of time of the Brownian motion
sigma ~ exponential(0.001)
sigma.setValue(0.1)

# univariate Brownian process along the tree
# parameterized by sigma
logmass ~ brownian(tau,sigma)

# moves on sigma 
index <- 1
moves[index] <- mScale(sigma, lambda=2.0, tune=true, weight=3.0)
index <- index + 1

# moves on the Brownian process
#moves[index] <- mvRealNodeValTreeSliding(process=logmass,lambda=10,tune=true,weight=100) 
#index <- index + 1
#moves[index] <- mvRealNodeValTreeTranslation(process=logmass,lambda=1,tune=true,weight=1) 
#index <- index + 1

# condition Brownian model on quantitative trait data
logmass.clampAt(contData,1)

# get useful functions of the covariance matrix, to be monitored:

# the mean and the standard deviation of log body mass across the tree
meanlogmass := logmass.mean()
stdevlogmass := logmass.stdev()

# the value of log body mass at the root
rootlogmass := logmass.rootVal()

"model"
# create the model
mymodel <- model(sigma)

# create the monitors
# some of them are a bit redundant, but just to show what can be done

"monitors"
# on screen, we will monitor only the correlation coefficient and the mean value of each trait
monitors[1] <- screenmonitor(printgen=1000, sigma, rootlogmass, meanlogmass, stdevlogmass)

# a trace monitor: like the screen monitor, but directly into file
monitors[2] <- filemonitor(filename="output/archaea.trace", printgen=100, separator = "	", sigma, rootlogmass, meanlogmass, stdevlogmass)

# a file monitor for the evolution of body mass along the tree (in newick format)
monitors[3] <- filemonitor(filename="output/archaea.traits",printgen=100, separator = "	", logmass)

# a model monitor
monitors[4] <- modelmonitor(filename="output/archaea.log",printgen=100, separator = "	")
 
mymcmc <- mcmc(mymodel, monitors, moves)

mymcmc.burnin(generations=100,tuningInterval=100)

mymcmc.run(100000)

# some post analysis here

