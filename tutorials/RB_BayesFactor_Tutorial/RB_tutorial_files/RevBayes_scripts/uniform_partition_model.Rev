################################################################################
#
# RevBayes Exercise: Calculating Marginal Liklihoods and Bayes Factors to test
#  data partitioning strategies
# 
# This file: Specifies the full model parameters and moves for a single, uniform
#				partition model
#
# authors: Tracy A. Heath, Michael Landis, and Sebastian Hoehna
#
################################################################################

# Read in sequence data for both genes #
data_atpB <- readCharacterData("data/conifer_atpB.nex")
data_rbcL <- readCharacterData("data/conifer_rbcL.nex")

# Create concatenated data matrix
data <- data_atpB + data_rbcL

# Get some useful variables from the data needed for specifying the model #
n_species <- data.ntaxa()
names <- data.names()
n_branches <- 2 * n_species - 3

#################################
#   Substitution Model: GTR+G   #
#   uniform over all sites      #
#################################
# Exchangeability rates P. 14, 15 #
er_prior <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_prior)
moves[1] <- mvSimplexElementScale(er, alpha=10.0, tune=true, weight=3.0)

# Stationary base frequencies P. 16 #
sf_prior <- v(1,1,1,1) 
sf ~ dnDirichlet(sf_prior)
moves[2] <- mvSimplexElementScale(sf, alpha=10.0, tune=true, weight=2.0)

# Instantaneous rate matrix (deterministic) P. 16 #
Q := fnGTR(er,sf) 

# Gamma-dist site rates P. 17 #
shape_prior <- 0.05
shape ~ dnExponential( shape_prior )
gamma_rates := fnDiscretizeGamma( shape, shape, 4 )
moves[3] <- mvScale(shape, lambda=0.8, tune=true, weight=3.0)


######################################
#   Tree topology & Branch lengths   #
#   shared for all sites             #
######################################
# Unrooted tree topology distribution P. 18 #
topology ~ dnUniformTopology(n_species, names)

# Tree topology moves #
moves[4] <- mvNNI(topology, weight=10.0)
moves[5] <- mvSPR(topology, weight=5.0)

#### Specify a prior and moves on the branch lengths #### 
# Create a vector of branch-length variables using a for loop #
mi <- 5 ## current index for the move vector
for (i in 1:n_branches) {
  br_lens[i] ~ dnExponential(10.0)
  moves[mi++] <- mvScale(br_lens[i],lambda=1,tune=true,weight=1) 
}

# A deterministic node for the tree length #
tree_length := sum(br_lens)

# Build the tree by combining the topology with the vector of branch lengths #
phylogeny := treeAssembly(topology, br_lens)


### Create the DAG P. 19 ###
# The sequence evolution model links the tree and GTR+G parameter #
phyloSeq ~ dnPhyloCTMC(tree=phylogeny, Q=Q, siteRates=gamma_rates, nSites=data.nchar(1), type="DNA")

# Attach the data #
phyloSeq.clamp(data)

# Specify the full model by indicating a single node in the DAG #
mymodel <- model(sf)


