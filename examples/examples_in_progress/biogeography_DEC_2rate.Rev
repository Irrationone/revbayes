############
# Settings #
############

# analysis input/output
in_fp           <- "/Users/mlandis/Documents/code/revbayes-code/examples/data/"
data_fn         <- "psychotria_range.nex"
area_fn         <- "hawaii_dynamic.atlas.txt"
out_fp          <- "/Users/mlandis/data/bayarea/output/"
out_str         <- "bg_2rate"

# input objects
vdata           <- readCharacterData(in_fp + data_fn)
atlas           <- readAtlas(in_fp + area_fn)
vtree           <- readTrees(in_fp + data_fn)
tree			<- vtree[1]
data			<- vdata

# MCMC settings
nGens           <- 10000
fBurn           <- 0.2



###########
#  Model  #
###########

# Per-area gain and loss rates 
#   glr[1]: 1->0, glr[2]: 0->1
for (i in 1:2) { glr[i] ~ dnExponential(20.0) }

# Distance-dependence
#    dp is the distance-dependence power parameter
dp          ~ dnExponential(10.0)
grm         := fnBiogeoGRM(atlas=atlas, distancePower=dp, useAdjacency=true, useAvailable=true, useDistance=true)

# Q_like is used to compute likelihoods
Q_like      := fnBiogeoDE(gainLossRates=glr, geoRateMod=grm, numAreas=4, forbidExtinction=true)

# M is focal model, the range data given the tree, the atlas, and the DEC process
M           ~  dnPhyloDACTMC(tree=tree, Q=Q_like, type="biogeo", forbidExtinction=true, useCladogenesis=true)

# attach observation to M
M.clamp(data)

# create the model!
my_model    <- model(glr)



#########
# Moves #
#########

n_nodes  <- tree.nnodes()

# standard parameter moves
moves[1] <- mvScale(x=glr[1], lambda=0.5, tune=false, weight=2.0)
moves[2] <- mvScale(x=glr[2], lambda=0.5, tune=false, weight=2.0)
moves[3] <- mvScale(x=dp, 	  lambda=0.5, tune=false, weight=2.0)

# data augmentation moves
moves[4] <- mvNodeCHRS(ctmc=M, qmap=Q_like, tree=tree, lambda=0.2, type="biogeo", weight=2.0*n_nodes)
moves[5] <- mvPathCHRS(ctmc=M, qmap=Q_like, tree=tree, lambda=0.2, type="biogeo", weight=2.0*n_nodes)
moves[6] <- mvNodeCHRS(ctmc=M, qmap=Q_like, tree=tree, lambda=1.0, type="biogeo", weight=n_nodes)
moves[7] <- mvPathCHRS(ctmc=M, qmap=Q_like, tree=tree, lambda=1.0, type="biogeo", weight=n_nodes)



############
# Monitors #
############

# standard monitors
monitors[1] <- screenmonitor(printgen=10, glr, dp)
monitors[2] <- modelmonitor(filename=out_fp+out_str+".parameters.txt", printgen=10)

# character history monitors
monitors[3] <- mnCharHistoryNewick(filename=out_fp+out_str+".events.txt", ctmc=M, tree=tree, printgen=10, style="events")
monitors[4] <- mnCharHistoryNewick(filename=out_fp+out_str+".counts.txt", ctmc=M, tree=tree, printgen=10, style="counts")
monitors[5] <- mnCharHistoryNhx(filename=out_fp+out_str+".nhx.txt", ctmc=M, tree=tree, atlas=atlas, samplegen=10, maxgen=nGens, burnin=0.25)
